import com.wiredforcode.gradle.spawn.*

plugins {
    id 'java'
    id 'application'
    id 'groovy'
    id 'idea'
    id 'com.github.lkishalmi.gatling' version '0.4.1'
    id 'com.wiredforcode.spawn' version '0.8.2'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}
repositories {
    jcenter()
    maven {
        url 'http://maven.opengamma.com/nexus/content/groups/public'
    }
}


sourceSets {

    main.java.srcDirs = []
    main.groovy.srcDirs += ['src/main/java']

    test.java.srcDirs = []
    test.groovy.srcDirs += ['src/test-commit/java', 'src/test-commit/groovy']

    testAcceptance {
        groovy {
            srcDirs += ['src/test-acceptance/java', 'src/test-acceptance/groovy']
            compileClasspath += test.compileClasspath
            runtimeClasspath += test.compileClasspath
        }
    }

    testPerformance {
        java {
            srcDirs = ['src/test-performance/java', 'src/test-performance/groovy']
            compileClasspath += test.compileClasspath
            runtimeClasspath += test.compileClasspath
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'org.scala-lang:scala-library:2.11.8'

    compile 'io.advantageous.qbit:qbit-vertx:1.12.0'
    compile 'ch.qos.logback:logback-core:1.1.6'
    compile 'ch.qos.logback:logback-classic:1.1.6'
//    compile 'org.slf4j:slf4j-api:1.7.19'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    gatlingCompile 'io.gatling:gatling-app:2.2.5'
}

sourceCompatibility = '1.8'
mainClassName = 'org.bearmug.qbit.MainService'
jar {
    manifest {
        attributes 'Main-Class': 'org.bearmug.qbit.MainService'
    }
}



task startServer(type: SpawnProcessTask, dependsOn: 'shadowJar') {
    command "java -jar $projectDir/build/libs/portfolio-bench-all.jar"
    ready 'HttpServer Started'
}

task stopServer(type: KillProcessTask)

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

apply from: 'scripts/continuous-integration.gradle'